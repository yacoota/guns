// https://gitee.com/stylefeng/guns.git
group 'cn.stylefeng'
version '1.0.0'


subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    sourceCompatibility = targetJavaVersion
    targetCompatibility = targetJavaVersion

    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    configurations {
        all*.exclude group: 'commons-logging'
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    ext {
        spring_boot_version = "2.3.4.RELEASE"
        mysql_connector_java_version = "8.0.11"
        oracle_version = "11.2.0.3"
        sqlserver_version = "1.3.1"
        pgsql_version = "42.2.5"
        kaptcha_version = "2.3.2"
        beetl_version = "2.9.3"
        swagger_version = "2.9.2"
        jwt_version = "0.9.0"
        oshi_version = "3.9.1"
        just_auth_version = "1.9.5"
        activiti_version = "6.0.0"
        batik_version = "1.7"
        docker_img_version = "latest"
        kernel_version = "2.2.12"
        lombok_version = "1.18.12"
    }

    dependencies {
        implementation(platform('org.springframework.boot:spring-boot-dependencies:2.3.4.RELEASE')) {
            exclude group: "mysql", module: "mysql-connector-java"
        }

        implementation("cn.stylefeng.roses:kernel-core:${kernel_version}") {
            exclude group: "mysql", module: "mysql-connector-java"
        }
        implementation "cn.stylefeng.roses:kernel-validator:${kernel_version}"
        implementation "cn.stylefeng.roses:kernel-excel:${kernel_version}"
        implementation "mysql:mysql-connector-java:${mysql_connector_java_version}"
        implementation "com.ibeetl:beetl:${beetl_version}"
        implementation "io.jsonwebtoken:jjwt:${jwt_version}"
        implementation "cn.afterturn:easypoi-web:4.0.0"
        implementation "com.github.penggle:kaptcha:${kaptcha_version}"
        implementation "com.github.oshi:oshi-core:${oshi_version}"
        implementation "commons-codec:commons-codec:1.11"
        implementation "commons-fileupload:commons-fileupload:1.3.3"
        implementation "io.springfox:springfox-swagger2:${swagger_version}"
        implementation "io.springfox:springfox-swagger-ui:${swagger_version}"

        compileOnly "org.projectlombok:lombok:${lombok_version}"
        annotationProcessor "org.projectlombok:lombok:${lombok_version}"
        testCompileOnly "org.projectlombok:lombok:${lombok_version}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombok_version}"

        testCompile "org.springframework.boot:spring-boot-starter-test"

        implementation "se.transmode.gradle:gradle-docker:1.2"
    }


    def profileName = System.getProperty("profile") ?: "dev"
    processResources {
        filter org.apache.tools.ant.filters.ReplaceTokens, tokens: ["spring.active": profileName]
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
            resources {
                srcDirs = ['src/main/webapp', 'src/main/resources']
            }
        }
    }

    jar {
        manifest {
            attributes("provider": "Gradle")
        }
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://maven.aliyun.com/nexus/content/groups/public" }
    }
    defaultTasks 'clean', 'build', 'assemble'
}
